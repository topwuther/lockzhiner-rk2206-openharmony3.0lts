# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//foundation/communication/dsoftbus/dsoftbus.gni")

if (defined(ohos_lite)) {
  import("//build/lite/config/component/lite_component.gni")

  config("dsoftbus_adapter_common_interface") {
    include_dirs = [
      "$softbus_adapter_common/include",
      "$softbus_adapter_config/spec_config",
    ]
  }

  common_include = [
    "$dsoftbus_root_path/core/common/include",
    "$hilog_lite_include_path",
    "$libsec_include_path",
    "//third_party/mbedtls/include",
    "//utils/native/base/include",
  ]

  if (ohos_kernel_type == "liteos_m") {
    static_library("softbus_adapter") {
      include_dirs = common_include
      include_dirs += [ "//kernel/liteos_m/kal/cmsis" ]
      cflags = [ "-Wall" ]
      sources = [
        "$softbus_adapter_common/bus_center/platform/bus_center_adapter_weak.c",
        "$softbus_adapter_common/kernel/liteos_m/softbus_adapter_file.c",
        "$softbus_adapter_common/kernel/liteos_m/softbus_adapter_mem.c",
        "$softbus_adapter_common/kernel/liteos_m/softbus_adapter_timer.c",
        "$softbus_adapter_common/log/softbus_adapter_log.c",
        "$softbus_adapter_common/mbedtls/softbus_adapter_crypto.c",
        "$softbus_adapter_config/spec_config/softbus_config_adapter.c",
      ]
      public_configs = [ ":dsoftbus_adapter_common_interface" ]
    }
  } else {
    shared_library("softbus_adapter") {
      include_dirs = common_include
      cflags = [
        "-Wall",
        "-fPIC",
      ]
      sources = [
        "$softbus_adapter_common/bus_center/platform/bus_center_adapter_weak.c",
        "$softbus_adapter_common/kernel/liteos_a/softbus_adapter_file.c",
        "$softbus_adapter_common/kernel/liteos_a/softbus_adapter_mem.c",
        "$softbus_adapter_common/kernel/liteos_a/softbus_adapter_timer.c",
        "$softbus_adapter_common/log/softbus_adapter_log.c",
        "$softbus_adapter_common/mbedtls/softbus_adapter_crypto.c",
        "$softbus_adapter_config/spec_config/softbus_config_adapter.c",
      ]
      deps = [
        "$hilog_lite_deps_path",
        "$libsec_deps_path",
        "//third_party/bounds_checking_function:libsec_shared",
      ]
      deps += [ "//third_party/mbedtls:mbedtls_shared" ]
      public_configs = [ ":dsoftbus_adapter_common_interface" ]
    }
  }
} else {
  import("//build/ohos.gni")

  config("config_adapter_common") {
    include_dirs = [
      "$softbus_adapter_common/include",
      "$softbus_adapter_config/spec_config",
      "$dsoftbus_root_path/core/common/include",
    ]
  }

  ohos_shared_library("softbus_adapter") {
    include_dirs = [
      "$dsoftbus_root_path/interfaces/kits",
      "$dsoftbus_root_path/interfaces/kits/common",
      "//base/startup/syspara_lite/interfaces/innerkits/native/syspara/include",
      "//third_party/mbedtls/include",
      "//utils/native/base/include",
    ]
    sources = [
      "$softbus_adapter_common/bus_center/platform/bus_center_adapter.c",
      "$softbus_adapter_common/kernel/liteos_a/softbus_adapter_file.c",
      "$softbus_adapter_common/kernel/liteos_a/softbus_adapter_mem.c",
      "$softbus_adapter_common/kernel/liteos_a/softbus_adapter_timer.c",
      "$softbus_adapter_common/log/softbus_adapter_log.c",
      "$softbus_adapter_common/mbedtls/softbus_adapter_crypto.c",
      "$softbus_adapter_config/spec_config/softbus_config_adapter.c",
    ]
    public_deps = [
      "$dsoftbus_root_path/components/mbedtls:mbedtls_shared",
      "//base/startup/syspara_lite/interfaces/innerkits/native/syspara:syspara",
      "//third_party/bounds_checking_function:libsec_static",
      "//utils/native/base:utils",
    ]
    public_configs = [ ":config_adapter_common" ]
    if (is_standard_system) {
      external_deps = [ "hiviewdfx_hilog_native:libhilog" ]
    }
    part_name = "dsoftbus_standard"
    subsystem_name = "communication"
  }
}
