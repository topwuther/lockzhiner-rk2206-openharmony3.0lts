# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//foundation/communication/dsoftbus/dsoftbus.gni")

config("bus_center_monitor_interface") {
  include_dirs = [ "include" ]
}
if (defined(ohos_lite)) {
  import("//build/lite/config/component/lite_component.gni")
  if (ohos_kernel_type == "liteos_m") {
    static_library("dsoftbus_bus_center_monitor") {
      sources = [
        "network/lnn_lwip_monitor.c",
        "network/lnn_netlink_monitor_virtual.c",
        "platform/lnn_product_monitor_virtual.c",
        "wlan/lnn_wifiservice_monitor_virtual.cpp",
      ]
      include_dirs = [
        "$dsoftbus_root_path/core/adapter/kernel/include",
        "$dsoftbus_root_path/core/common/include",
        "$libsec_include_path",
        "$hilog_lite_include_path",
        "$hispark_pegasus_sdk_path/third_party/lwip_sack/include/lwip",
      ]
      cflags = [
        "-Wall",
        "-fPIC",
        "-fno-builtin",
        "-std=c99",
      ]
      public_configs = [ ":bus_center_monitor_interface" ]
      deps = [
        "$dsoftbus_root_path/core/adapter/kernel:softbus_adapter_kernel",
        "$dsoftbus_root_path/core/bus_center/utils:dsoftbus_bus_center_utils",
        "$hilog_lite_deps_path",
        "$libsec_deps_path",
      ]
    }
  } else {
    shared_library("dsoftbus_bus_center_monitor") {
      if (ohos_kernel_type == "linux") {
        sources = [
          "network/lnn_lwip_monitor_virtual.c",
          "network/lnn_netlink_monitor.c",
          "platform/lnn_product_monitor_virtual.c",
          "wlan/lnn_wifiservice_monitor_virtual.cpp",
        ]
      }
      include_dirs = [
        "$softbus_adapter_common/include",
        "$dsoftbus_root_path/core/common/include",
        "//third_party/bounds_checking_function/include",
        "$hilog_lite_include_path",
      ]
      if (ohos_kernel_type == "liteos_a") {
        sources = [
          "network/lnn_lwip_monitor_virtual.c",
          "network/lnn_netlink_monitor_virtual.c",
          "platform/lnn_product_monitor.c",
          "wlan/lnn_wifiservice_monitor_virtual.cpp",
        ]
        include_dirs += [
          "//drivers/adapter/uhdf/posix/include",
          "//drivers/framework/include/utils",
          "//drivers/framework/include/core",
          "//drivers/framework/ability/sbuf/include",
        ]
      }
      cflags = [
        "-Wall",
        "-Werror",
        "-fPIC",
        "-fno-builtin",
        "-std=c99",
      ]
      public_configs = [ ":bus_center_monitor_interface" ]
      deps = [
        "$dsoftbus_root_path/adapter:softbus_adapter",
        "$dsoftbus_root_path/core/bus_center/utils:dsoftbus_bus_center_utils",
        "$dsoftbus_root_path/core/common/log:softbus_log",
        "$hilog_lite_deps_path",
        "//third_party/bounds_checking_function:libsec_shared",
      ]
      if (ohos_kernel_type == "liteos_a") {
        deps += [
          "//drivers/adapter/uhdf/manager:hdf_core",
          "//drivers/adapter/uhdf/platform:hdf_platform",
          "//drivers/adapter/uhdf/posix:hdf_posix_osal",
        ]
      }
    }
  }
} else {
  import("//build/ohos.gni")

  ohos_shared_library("dsoftbus_bus_center_monitor") {
    sources = [
      "network/lnn_lwip_monitor_virtual.c",
      "network/lnn_netlink_monitor.c",
      "platform/lnn_product_monitor_virtual.c",
      "wlan/lnn_wifiservice_monitor.cpp",
    ]
    include_dirs = [
      "$softbus_adapter_common/include",
      "$dsoftbus_root_path/core/common/include",
      "//utils/native/base/include",
      "//foundation/communication/wifi/interfaces/innerkits/native_cpp/wifi_standard/interfaces",
      "//foundation/communication/wifi/services/wifi_standard/wifi_framework/common/net_helper",
    ]
    public_configs = [ ":bus_center_monitor_interface" ]
    deps = [
      "$dsoftbus_root_path/adapter:softbus_adapter",
      "$dsoftbus_root_path/core/bus_center/utils:dsoftbus_bus_center_utils",
      "$dsoftbus_root_path/core/common/log:softbus_log",
      "$dsoftbus_root_path/core/common/message_handler:message_handler",
      "//foundation/aafwk/standard/interfaces/innerkits/want:want",
      "//utils/native/base:utils",
    ]
    if (is_standard_system) {
      external_deps = [
        "ces_standard:cesfwk_innerkits",
        "hiviewdfx_hilog_native:libhilog",
        "ipc:ipc_core",
      ]
    }
    part_name = "dsoftbus_standard"
    subsystem_name = "communication"
  }
}
