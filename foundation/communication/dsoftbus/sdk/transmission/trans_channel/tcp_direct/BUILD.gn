# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//foundation/communication/dsoftbus/dsoftbus.gni")

config("trans_direct_channel_sdk_interface") {
  include_dirs = [
    "include",
    "$dsoftbus_core_path/common/security/sequence_verification/include",
    "$dsoftbus_root_path/core/common/softbus_property/include",
    "$dsoftbus_root_path/interfaces/kits/common",
  ]
}

common_src = [
  "src/client_trans_tcp_direct_callback.c",
  "src/client_trans_tcp_direct_listener.c",
  "src/client_trans_tcp_direct_manager.c",
  "src/client_trans_tcp_direct_message.c",
]

if (defined(ohos_lite)) {
  import("//build/lite/config/component/lite_component.gni")

  common_deps = [
    "$dsoftbus_core_path/common/security/sequence_verification:softbus_seq_verification",
    "$dsoftbus_root_path/adapter:softbus_adapter",
    "$dsoftbus_root_path/core/common:softbus_core_common",
    "$dsoftbus_root_path/core/common/log:softbus_log",
    "$dsoftbus_root_path/core/common/softbus_property:softbus_property",
    "$dsoftbus_root_path/core/connection/common:conn_common",
    "$dsoftbus_root_path/core/transmission/pending_packet:softbus_trans_pending",
  ]

  common_include = [
    "$dsoftbus_root_path/interfaces/kits/transport",
    "$softbus_adapter_common/include",
    "$dsoftbus_root_path/core/common/include",
    "$dsoftbus_root_path/core/connection/interface",
    "$dsoftbus_root_path/core/transmission/common/include",
    "$dsoftbus_root_path/core/authentication/interface",
    "$dsoftbus_root_path/sdk/transmission/session/include",
    "$libsec_include_path",
    "$hilog_lite_include_path",
  ]

  if (ohos_kernel_type == "liteos_m") {
    static_library("dsoftbus_trans_direct_channel_sdk") {
      include_dirs = common_include
      sources = common_src
      public_configs = [ ":trans_direct_channel_sdk_interface" ]
      deps = common_deps
    }
  } else {
    shared_library("dsoftbus_trans_direct_channel_sdk") {
      include_dirs = common_include
      cflags = [
        "-Wall",
        "-fPIC",
      ]
      sources = common_src
      public_configs = [ ":trans_direct_channel_sdk_interface" ]
      deps = common_deps
      deps += [
        "$hilog_lite_deps_path",
        "$libsec_deps_path",
      ]
    }
  }
} else {
  import("//build/ohos.gni")

  ohos_shared_library("dsoftbus_trans_direct_channel_sdk") {
    include_dirs = [
      "$dsoftbus_root_path/interfaces/kits/transport",
      "$softbus_adapter_common/include",
      "$dsoftbus_root_path/core/common/include",
      "$dsoftbus_root_path/core/common/security/sequence_verification/include",
      "$dsoftbus_root_path/core/common/wifi_common/tcp_socket/include",
      "$dsoftbus_root_path/core/connection/interface",
      "$dsoftbus_root_path/core/transmission/common/include",
      "$dsoftbus_root_path/core/authentication/interface",
      "$dsoftbus_root_path/sdk/transmission/session/include",
    ]
    sources = common_src
    public_configs = [ ":trans_direct_channel_sdk_interface" ]
    deps = [
      "$dsoftbus_core_path/common/security/sequence_verification:softbus_seq_verification",
      "$dsoftbus_root_path/adapter:softbus_adapter",
      "$dsoftbus_root_path/core/common/log:softbus_log",
      "$dsoftbus_root_path/core/common/softbus_property:softbus_property",
      "$dsoftbus_root_path/core/common/utils:softbus_utils",
      "$dsoftbus_root_path/core/connection/common:conn_common",
      "$dsoftbus_root_path/core/transmission/pending_packet:softbus_trans_pending",
    ]
    if (is_standard_system) {
      external_deps = [ "hiviewdfx_hilog_native:libhilog" ]
    }
    part_name = "dsoftbus_standard"
    subsystem_name = "communication"
  }
}
