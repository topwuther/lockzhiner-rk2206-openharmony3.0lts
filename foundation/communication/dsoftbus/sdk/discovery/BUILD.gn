# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//foundation/communication/dsoftbus/dsoftbus.gni")

config("disc_client_interface") {
  include_dirs = [
    "manager/include",
    "$dsoftbus_root_path/interfaces/kits/discovery",
    "$dsoftbus_root_path/interfaces/kits/common",
    "$dsoftbus_root_path/core/connection/interface",
  ]
}

if (defined(ohos_lite)) {
  import("//build/lite/config/component/lite_component.gni")

  common_include = [
    "ipc/include",
    "$softbus_adapter_common/include",
    "$dsoftbus_root_path/core/common/include",
    "$dsoftbus_root_path/interfaces/innerkits/discovery",
    "$dsoftbus_root_path/core/common/inner_communication",
    "$dsoftbus_root_path/core/discovery/interface",
    "$libsec_include_path",
    "$hilog_lite_include_path",
  ]

  common_deps = [
    "$dsoftbus_root_path/adapter:softbus_adapter",
    "$dsoftbus_root_path/core/common/log:softbus_log",
    "$hilog_lite_deps_path",
  ]

  if (ohos_kernel_type == "liteos_m") {
    static_library("dsoftbus_disc_manager_sdk") {
      include_dirs = common_include
      sources = [
        "ipc/mini/disc_server_proxy.c",
        "manager/src/client_disc_manager.c",
      ]
      public_configs = [ ":disc_client_interface" ]
      deps = common_deps
      deps += [ "$dsoftbus_root_path/core/discovery:dsoftbus_disc_server" ]
    }
  } else {
    shared_library("dsoftbus_disc_manager_sdk") {
      include_dirs = common_include
      sources = [
        "ipc/small/disc_server_proxy.c",
        "manager/src/client_disc_manager.c",
      ]
      public_configs = [ ":disc_client_interface" ]
      deps = common_deps
      deps += [
        "$libsec_deps_path",
        "//foundation/communication/ipc_lite:liteipc_adapter",
        "//foundation/distributedschedule/samgr_lite/samgr:samgr",
      ]
    }
  }
} else {
  import("//build/ohos.gni")

  ohos_shared_library("dsoftbus_disc_manager_sdk") {
    include_dirs = [
      "ipc/include",
      "ipc/standard/include",
      "$dsoftbus_root_path/core/common/include",
      "$dsoftbus_root_path/core/frame/standard/server/include",
      "$dsoftbus_root_path/core/transmission/common/include",
      "$dsoftbus_root_path/interfaces/kits/transport",
      "//utils/system/safwk/native/include",
      "$softbus_adapter_common/include",
    ]
    sources = [
      "$dsoftbus_root_path/sdk/discovery/ipc/standard/src/disc_server_proxy.cpp",
      "$dsoftbus_root_path/sdk/discovery/ipc/standard/src/disc_server_proxy_standard.cpp",
      "manager/src/client_disc_manager.c",
    ]

    public_configs = [ ":disc_client_interface" ]

    if (is_standard_system) {
      external_deps = [
        "hiviewdfx_hilog_native:libhilog",
        "ipc:ipc_single",
      ]
      deps = [
        "$dsoftbus_root_path/adapter:softbus_adapter",
        "$dsoftbus_root_path/core/common/log:softbus_log",
        "//utils/native/base:utils",
      ]
    }
    part_name = "dsoftbus_standard"
    subsystem_name = "communication"
  }
}
