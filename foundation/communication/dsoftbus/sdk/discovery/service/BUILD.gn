# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//foundation/communication/dsoftbus/dsoftbus.gni")

config("disc_client_service_interface") {
  include_dirs = [
    "include",
    "$dsoftbus_root_path/interfaces/kits/discovery",
    "$dsoftbus_root_path/interfaces/kits/common",
  ]
}

if (defined(ohos_lite)) {
  import("//build/lite/config/component/lite_component.gni")

  common_include = [
    "include",
    "$dsoftbus_root_path/core/common/include",
    "$softbus_adapter_common/include",
    "$dsoftbus_root_path/interfaces/kits/common",
    "$dsoftbus_root_path/interfaces/kits/discovery",
    "$dsoftbus_root_path/sdk/discovery/manager/include",
    "$dsoftbus_root_path/sdk/frame/common/include",
    "$hilog_lite_include_path",
  ]

  common_deps = [
    "$dsoftbus_root_path/adapter:softbus_adapter",
    "$dsoftbus_root_path/core/common/log:softbus_log",
    "$dsoftbus_root_path/sdk/discovery:dsoftbus_disc_manager_sdk",
    "$dsoftbus_root_path/sdk/frame:softbus_client_frame",
    "$hilog_lite_deps_path",
  ]

  if (ohos_kernel_type == "liteos_m") {
    static_library("dsoftbus_disc_service_sdk") {
      include_dirs = common_include
      public_configs = [ ":disc_client_service_interface" ]
      sources = [ "src/client_disc_service.c" ]
      deps = common_deps
    }
  } else {
    shared_library("dsoftbus_disc_service_sdk") {
      include_dirs = common_include
      public_configs = [ ":disc_client_service_interface" ]
      sources = [ "src/client_disc_service.c" ]
      deps = common_deps
      cflags = [
        "-Wall",
        "-fPIC",
      ]
    }
  }
} else {
  import("//build/ohos.gni")

  ohos_shared_library("dsoftbus_disc_service_sdk") {
    include_dirs = [
      "include",
      "$dsoftbus_root_path/core/common/include",
      "$softbus_adapter_common/include",
      "$dsoftbus_root_path/interfaces/kits/common",
      "$dsoftbus_root_path/interfaces/kits/discovery",
      "$dsoftbus_root_path/sdk/discovery/manager/include",
      "$dsoftbus_root_path/sdk/frame/common/include",
    ]
    public_configs = [ ":disc_client_service_interface" ]
    sources = [ "src/client_disc_service.c" ]
    deps = [
      "$dsoftbus_root_path/adapter:softbus_adapter",
      "$dsoftbus_root_path/core/common/log:softbus_log",
      "$dsoftbus_root_path/sdk/discovery:dsoftbus_disc_manager_sdk",
      "$dsoftbus_root_path/sdk/frame:softbus_client_frame",
    ]
    if (is_standard_system) {
      external_deps = [ "hiviewdfx_hilog_native:libhilog" ]
    }
    part_name = "dsoftbus_standard"
    subsystem_name = "communication"
  }
}
