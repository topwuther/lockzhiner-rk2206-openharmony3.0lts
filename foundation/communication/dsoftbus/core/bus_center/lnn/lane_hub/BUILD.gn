# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//foundation/communication/dsoftbus/dsoftbus.gni")

config("bus_center_hub_interface") {
  include_dirs = [
    "include",
    "$dsoftbus_root_path/core/bus_center/interface",
    "$dsoftbus_root_path/interfaces/kits",
    "$dsoftbus_root_path/interfaces/kits/bus_center",
    "$dsoftbus_root_path/interfaces/kits/common",
  ]
}

if (defined(ohos_lite)) {
  import("//build/lite/config/component/lite_component.gni")
  if (ohos_kernel_type == "liteos_m") {
  } else {
    shared_library("dsoftbus_bus_center_hub") {
      sources = [
        "lane_manager/src/lnn_lane_info.c",
        "lane_manager/src/lnn_lane_manager.c",
        "lane_manager/src/lnn_smart_communication.c",
      ]
      include_dirs = [
        "lane_manager/include",
        "time_sync/include",
        "$dsoftbus_root_path/core/bus_center/lnn/net_ledger/common/include",
        "$dsoftbus_root_path/core/bus_center/service/include",
        "$softbus_adapter_common/include",
        "$dsoftbus_root_path/core/common/include",
        "//third_party/bounds_checking_function/include",
        "//utils/native/base/include",
        "$hilog_lite_include_path",
      ]
      cflags = [
        "-Wall",
        "-Werror",
        "-fPIC",
        "-fno-builtin",
        "-std=c99",
      ]
      public_configs = [ ":bus_center_hub_interface" ]
      deps = [
        "$dsoftbus_root_path/adapter:softbus_adapter",
        "$dsoftbus_root_path/core/bus_center/lnn/net_ledger:dsoftbus_bus_center_ledger",
        "$hilog_lite_deps_path",
        "//third_party/bounds_checking_function:libsec_shared",
      ]
      if (enable_time_sync == true) {
        sources += [ "time_sync/src/lnn_time_sync_manager.c" ]
        native_source_path = rebase_path("$dsoftbus_root_path")
        dep_file =
            "dsoftbus_enhance/core/bus_center/lnn/lane_hub/time_sync/BUILD.gn"
        enhanced = exec_script(
                "$dsoftbus_root_path/run_shell_cmd.py",
                [ "cd $native_source_path; if [ -f $dep_file ]; then echo true; else echo false; fi" ],
                "value")
        if (enhanced) {
          deps += [ "$dsoftbus_root_path/dsoftbus_enhance/core/bus_center/lnn/lane_hub/time_sync:dsoftbus_time_sync_impl" ]
        } else {
          sources += [ "time_sync/src/lnn_time_sync_impl_stub.c" ]
        }
        include_dirs += [
          "$dsoftbus_root_path/core/adapter/bus_center/include/",
          "$dsoftbus_root_path/core/bus_center/lnn/net_ledger/distributed_ledger/include",
          "$dsoftbus_root_path/core/common/message_handler/include",
        ]
        deps += [
          "$dsoftbus_root_path/core/common/message_handler:message_handler",
        ]
      } else {
        sources += [ "time_sync/src/lnn_time_sync_manager_stub.c" ]
      }
    }
  }
} else {
  import("//build/ohos.gni")

  ohos_shared_library("dsoftbus_bus_center_hub") {
    sources = [
      "lane_manager/src/lnn_lane_info.c",
      "lane_manager/src/lnn_lane_manager.c",
      "lane_manager/src/lnn_smart_communication.c",
    ]
    include_dirs = [
      "lane_manager/include",
      "time_sync/include",
      "$dsoftbus_root_path/core/bus_center/lnn/net_ledger/common/include",
      "$dsoftbus_root_path/core/bus_center/service/include",
      "$softbus_adapter_common/include",
      "$dsoftbus_root_path/core/common/include",
      "//utils/native/base/include",
    ]
    public_configs = [ ":bus_center_hub_interface" ]
    deps = [
      "$dsoftbus_root_path/adapter:softbus_adapter",
      "$dsoftbus_root_path/core/bus_center/lnn/net_ledger:dsoftbus_bus_center_ledger",
      "//utils/native/base:utils",
    ]
    if (enable_time_sync == true) {
      sources += [ "time_sync/src/lnn_time_sync_manager.c" ]
      native_source_path = rebase_path("$dsoftbus_root_path")
      dep_file =
          "dsoftbus_enhance/core/bus_center/lnn/lane_hub/time_sync/BUILD.gn"
      enhanced = exec_script(
              "$dsoftbus_root_path/run_shell_cmd.py",
              [ "cd $native_source_path; if [ -f $dep_file ]; then echo true; else echo false; fi" ],
              "value")
      if (enhanced) {
        deps += [ "$dsoftbus_root_path/dsoftbus_enhance/core/bus_center/lnn/lane_hub/time_sync:dsoftbus_time_sync_impl" ]
      } else {
        sources += [ "time_sync/src/lnn_time_sync_impl_stub.c" ]
      }
      include_dirs += [
        "$dsoftbus_root_path/core/adapter/bus_center/include/",
        "$dsoftbus_root_path/core/bus_center/lnn/net_ledger/distributed_ledger/include",
        "$dsoftbus_root_path/core/common/message_handler/include",
      ]
      deps +=
          [ "$dsoftbus_root_path/core/common/message_handler:message_handler" ]
    } else {
      sources += [ "time_sync/src/lnn_time_sync_manager_stub.c" ]
    }
    if (is_standard_system) {
      external_deps = [ "hiviewdfx_hilog_native:libhilog" ]
    }
    part_name = "dsoftbus_standard"
    subsystem_name = "communication"
  }
}
