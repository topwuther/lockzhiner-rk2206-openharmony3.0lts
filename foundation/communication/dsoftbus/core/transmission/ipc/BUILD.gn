# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//foundation/communication/dsoftbus/dsoftbus.gni")

config("trans_ipc_proxy_interface") {
  include_dirs = [
    "include",
    "$dsoftbus_root_path/interfaces/kits/common",
    "$dsoftbus_root_path/core/connection/interface",
  ]
}

if (defined(ohos_lite)) {
  import("//build/lite/config/component/lite_component.gni")

  common_include = [
    "$softbus_adapter_common/include",
    "$dsoftbus_root_path/core/common/include",
    "$dsoftbus_root_path/core/connection/interface",
    "$hilog_lite_include_path",
    "$libsec_include_path",
  ]

  common_deps = [
    "$dsoftbus_root_path/adapter:softbus_adapter",
    "$dsoftbus_root_path/core/common/log:softbus_log",
    "$dsoftbus_root_path/core/connection/common:conn_common",
  ]

  if (ohos_kernel_type == "liteos_m") {
    static_library("dsoftbus_trans_ipc_proxy") {
      include_dirs = common_include
      include_dirs += [
        "$dsoftbus_root_path/sdk/transmission/trans_channel/manager/include",
      ]
      deps = common_deps
      sources = [ "mini/trans_client_proxy.c" ]
      public_configs = [ ":trans_ipc_proxy_interface" ]
    }
  } else {
    shared_library("dsoftbus_trans_ipc_proxy") {
      include_dirs = common_include
      cflags = [
        "-Wall",
        "-fPIC",
      ]
      sources = [ "small/trans_client_proxy.c" ]
      public_configs = [ ":trans_ipc_proxy_interface" ]
      deps = common_deps
      deps += [
        "$dsoftbus_root_path/core/frame/small/client_manager:client_manager",
        "$hilog_lite_deps_path",
        "$libsec_deps_path",
        "//foundation/communication/ipc_lite:liteipc_adapter",
      ]
    }
  }
} else {
  import("//build/ohos.gni")

  ohos_shared_library("dsoftbus_trans_ipc_proxy") {
    include_dirs = [
      "standard/include",
      "$dsoftbus_root_path/core/common/include",
      "$dsoftbus_root_path/core/frame/standard/client/include",
      "$dsoftbus_root_path/core/frame/standard/softbusdata/include",
      "$dsoftbus_root_path/interfaces/kits/discovery",
      "$dsoftbus_root_path/interfaces/kits/common",
    ]
    sources = [
      "standard/src/trans_client_proxy.cpp",
      "standard/src/trans_client_proxy_standard.cpp",
    ]
    public_configs = [ ":trans_ipc_proxy_interface" ]
    if (is_standard_system) {
      external_deps = [
        "hiviewdfx_hilog_native:libhilog",
        "ipc:ipc_core",
      ]
    }
    deps = [
      "$dsoftbus_root_path/core/common/log:softbus_log",
      "$dsoftbus_root_path/core/frame/standard/softbusdata:softbus_server_data",
      "//utils/native/base:utils",
    ]
    part_name = "dsoftbus_standard"
    subsystem_name = "communication"
  }
}
