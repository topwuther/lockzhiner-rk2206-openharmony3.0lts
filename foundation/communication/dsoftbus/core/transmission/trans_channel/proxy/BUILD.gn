# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//foundation/communication/dsoftbus/dsoftbus.gni")

config("trans_proxy_channel_interface") {
  include_dirs = [
    "include",
    "$dsoftbus_root_path/interfaces/kits/transport",
    "$dsoftbus_root_path/interfaces/kits/common",
  ]
}

common_include = [
  "$dsoftbus_root_path/core/transmission/trans_channel/manager/include",
  "$dsoftbus_root_path/core/common/include",
  "$dsoftbus_root_path/core/transmission/interface",
  "$dsoftbus_root_path/core/transmission/common/include",
  "$dsoftbus_root_path/core/connection/interface",
  "$dsoftbus_root_path/core/connection/manager",
  "$dsoftbus_root_path/core/authentication/interface",
  "$dsoftbus_root_path/core/bus_center/interface",
  "$dsoftbus_root_path/interfaces/kits/bus_center",
  "$dsoftbus_root_path/interfaces/kits/common",
  "$dsoftbus_root_path/core/bus_center/interface",
  "$softbus_adapter_common/include",
  "//third_party/cJSON",
]

common_src = [
  "src/softbus_proxychannel_callback.c",
  "src/softbus_proxychannel_control.c",
  "src/softbus_proxychannel_listener.c",
  "src/softbus_proxychannel_manager.c",
  "src/softbus_proxychannel_message.c",
  "src/softbus_proxychannel_network.c",
  "src/softbus_proxychannel_session.c",
  "src/softbus_proxychannel_transceiver.c",
]

common_deps = [
  "$dsoftbus_core_path/common/json_utils:json_utils",
  "$dsoftbus_core_path/common/message_handler:message_handler",
  "$dsoftbus_core_path/transmission/common:dsoftbus_trans_common",
  "$dsoftbus_core_path/transmission/ipc:dsoftbus_trans_ipc_proxy",
  "$dsoftbus_root_path/adapter:softbus_adapter",
  "$dsoftbus_root_path/core/transmission/pending_packet:softbus_trans_pending",
]

if (defined(ohos_lite)) {
  import("//build/lite/config/component/lite_component.gni")

  common_include += [
    "$hilog_lite_include_path",
    "$libsec_include_path",
  ]

  if (ohos_kernel_type == "liteos_m") {
    static_library("dsoftbus_trans_proxy_channel") {
      include_dirs = common_include
      include_dirs +=
          [ "$dsoftbus_root_path/core/common/message_handler/include" ]
      sources = common_src
      public_configs = [ ":trans_proxy_channel_interface" ]
      deps = common_deps
      deps += [ "//build/lite/config/component/cJSON:cjson_static" ]
    }
  } else {
    shared_library("dsoftbus_trans_proxy_channel") {
      include_dirs = common_include
      cflags = [
        "-Wall",
        "-fPIC",
      ]
      sources = common_src
      public_configs = [ ":trans_proxy_channel_interface" ]
      deps = common_deps
      deps += [
        "$hilog_lite_deps_path",
        "$libsec_deps_path",
        "//build/lite/config/component/cJSON:cjson_shared",
      ]
    }
  }
} else {
  import("//build/ohos.gni")

  ohos_shared_library("dsoftbus_trans_proxy_channel") {
    include_dirs = common_include
    include_dirs += [
      "$dsoftbus_root_path/core/transmission/trans_channel/manager/include",
    ]
    sources = common_src
    public_configs = [ ":trans_proxy_channel_interface" ]
    deps = common_deps
    deps += [
      "$dsoftbus_core_path/common/utils:softbus_utils",
      "$dsoftbus_root_path/core/authentication:dsoftbus_auth_server",
      "$dsoftbus_root_path/core/bus_center/lnn/net_ledger:dsoftbus_bus_center_ledger",
      "$dsoftbus_root_path/core/common/message_handler",
      "$dsoftbus_root_path/core/connection/manager:conn_manager",
    ]
    if (is_standard_system) {
      external_deps = [ "hiviewdfx_hilog_native:libhilog" ]
    }
    part_name = "dsoftbus_standard"
    subsystem_name = "communication"
  }
}
