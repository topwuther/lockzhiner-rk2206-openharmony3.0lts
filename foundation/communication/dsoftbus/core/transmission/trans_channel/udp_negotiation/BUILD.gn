# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//foundation/communication/dsoftbus/dsoftbus.gni")

config("trans_udp_channel_interface") {
  include_dirs = [
    "include",
    "$dsoftbus_root_path/core/transmission/trans_channel/manager/include",
    "$dsoftbus_root_path/core/bus_center/lnn/lane_hub/lane_manager/include",
    "$dsoftbus_root_path/core/transmission/common",
    "$dsoftbus_root_path/core/bus_center/interface",
    "$dsoftbus_root_path/interfaces/kits/transport",
    "$dsoftbus_root_path/interfaces/kits/common",
  ]
}

if (enable_trans_udp == true) {
  common_src = [
    "src/trans_udp_channel_manager.c",
    "src/trans_udp_negotiation.c",
    "src/trans_udp_negotiation_exchange.c",
  ]
} else {
  common_src = [ "src/trans_udp_virtual.c" ]
}

if (defined(ohos_lite)) {
  import("//build/lite/config/component/lite_component.gni")

  if (enable_trans_udp == true) {
    common_deps = [
      "$dsoftbus_root_path/adapter:softbus_adapter",
      "$dsoftbus_root_path/core/authentication:dsoftbus_auth_server",
      "$dsoftbus_root_path/core/bus_center/lnn/net_ledger:dsoftbus_bus_center_ledger",
      "$dsoftbus_root_path/core/common/json_utils:json_utils",
      "$dsoftbus_root_path/core/common/utils:softbus_utils",
      "$hilog_lite_deps_path",
      "$libsec_deps_path",
    ]
  } else {
    common_deps = []
  }

  common_include = [
    "include",
    "$softbus_adapter_common/include",
    "$dsoftbus_root_path/core/authentication/interface",
    "$dsoftbus_root_path/core/common/include",
    "$dsoftbus_root_path/core/connection/interface",
    "$dsoftbus_root_path/core/transmission/common/include",
    "$dsoftbus_root_path/core/transmission/ipc/include",
    "$dsoftbus_root_path/interfaces/kits/bus_center",
    "$dsoftbus_root_path/interfaces/kits/common",
    "//third_party/cJSON",
    "$hilog_lite_include_path",
    "$libsec_include_path",
  ]

  if (ohos_kernel_type == "liteos_m") {
    static_library("dsoftbus_trans_udp_channel") {
      include_dirs = common_include
      sources = common_src
      public_configs = [ ":trans_udp_channel_interface" ]
      deps = common_deps
      deps += [ "//build/lite/config/component/cJSON:cjson_static" ]
    }
  } else {
    shared_library("dsoftbus_trans_udp_channel") {
      sources = common_src
      include_dirs = common_include
      public_configs = [ ":trans_udp_channel_interface" ]
      cflags = [
        "-Wall",
        "-std=c99",
      ]
      deps = common_deps
    }
  }
} else {
  import("//build/ohos.gni")

  ohos_shared_library("dsoftbus_trans_udp_channel") {
    include_dirs = [
      "$dsoftbus_root_path/core/common/include",
      "$dsoftbus_root_path/core/transmission/common/include",
      "//third_party/cJSON",
      "$softbus_adapter_common/include",
      "$dsoftbus_root_path/core/connection/interface",
      "$dsoftbus_root_path/interfaces/kits/common",
      "$dsoftbus_root_path/core/common/wifi_common/base_listener/include",
      "$dsoftbus_root_path/core/common/wifi_common/tcp_socket/include",
      "$dsoftbus_root_path/core/bus_center/interface",
      "$dsoftbus_root_path/interfaces/kits/bus_center",
      "$dsoftbus_root_path/core/authentication/interface",
      "$dsoftbus_root_path/core/transmission/session/include",
    ]
    sources = common_src
    public_configs = [ ":trans_udp_channel_interface" ]

    deps = [
      "$dsoftbus_core_path/common/utils:softbus_utils",
      "$dsoftbus_core_path/connection/common:conn_common",
      "$dsoftbus_core_path/transmission/common:dsoftbus_trans_common",
      "$dsoftbus_core_path/transmission/ipc:dsoftbus_trans_ipc_proxy",
      "$dsoftbus_root_path/adapter:softbus_adapter",
      "$dsoftbus_root_path/core/authentication:dsoftbus_auth_server",
      "$dsoftbus_root_path/core/bus_center/lnn/net_ledger:dsoftbus_bus_center_ledger",
      "//third_party/cJSON:cjson_static",
    ]
    if (is_standard_system) {
      external_deps = [ "hiviewdfx_hilog_native:libhilog" ]
    }
    part_name = "dsoftbus_standard"
    subsystem_name = "communication"
  }
}
