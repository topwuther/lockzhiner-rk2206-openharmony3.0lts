# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//foundation/communication/dsoftbus/dsoftbus.gni")

config("trans_common_interface") {
  include_dirs = [
    "include",
    "$dsoftbus_root_path/interfaces/kits/transport",
    "$dsoftbus_root_path/interfaces/kits/common",
  ]
}

if (defined(ohos_lite)) {
  import("//build/lite/config/component/lite_component.gni")

  common_include = [
    "$dsoftbus_root_path/core/common/include",
    "//third_party/cJSON",
    "$softbus_adapter_common/include",
    "$hilog_lite_include_path",
    "$libsec_include_path",
  ]

  common_deps = [
    "$dsoftbus_root_path/adapter:softbus_adapter",
    "$dsoftbus_root_path/core/common/json_utils:json_utils",
    "$dsoftbus_root_path/core/common/log:softbus_log",
  ]

  if (ohos_kernel_type == "liteos_m") {
    static_library("dsoftbus_trans_common") {
      include_dirs = common_include
      sources = [ "src/softbus_message_open_channel.c" ]
      public_configs = [ ":trans_common_interface" ]
      deps = common_deps
      deps += [ "//build/lite/config/component/cJSON:cjson_static" ]
    }
  } else {
    shared_library("dsoftbus_trans_common") {
      include_dirs = common_include
      cflags = [
        "-Wall",
        "-fPIC",
      ]
      sources = [ "src/softbus_message_open_channel.c" ]
      deps = common_deps
      deps += [
        "$hilog_lite_deps_path",
        "$libsec_deps_path",
        "//build/lite/config/component/cJSON:cjson_shared",
      ]
      public_configs = [ ":trans_common_interface" ]
    }
  }
} else {
  import("//build/ohos.gni")

  ohos_shared_library("dsoftbus_trans_common") {
    include_dirs = [
      "$dsoftbus_root_path/core/common/include",
      "//third_party/cJSON",
      "$dsoftbus_root_path/sdk/transmission/trans_channel/tcp_direct",
      "$softbus_adapter_common/include",
    ]
    sources = [ "src/softbus_message_open_channel.c" ]
    public_configs = [ ":trans_common_interface" ]
    if (is_standard_system) {
      external_deps = [ "hiviewdfx_hilog_native:libhilog" ]
    }
    public_deps = [
      "$dsoftbus_root_path/adapter:softbus_adapter",
      "$dsoftbus_root_path/core/common/json_utils",
      "$dsoftbus_root_path/core/common/log:softbus_log",
    ]
    part_name = "dsoftbus_standard"
    subsystem_name = "communication"
  }
}
