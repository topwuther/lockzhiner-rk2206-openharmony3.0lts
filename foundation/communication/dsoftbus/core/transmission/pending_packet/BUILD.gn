# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//foundation/communication/dsoftbus/dsoftbus.gni")

config("softbus_trans_pending_interface") {
  include_dirs = [ "include" ]
}

if (defined(ohos_lite)) {
  import("//build/lite/config/component/lite_component.gni")
  common_include = [
    "$dsoftbus_root_path/core/common/include",
    "$hilog_lite_include_path",
    "$softbus_adapter_common/include",
  ]
  common_deps = [
    "$dsoftbus_root_path/adapter:softbus_adapter",
    "$dsoftbus_root_path/core/common/log:softbus_log",
    "$dsoftbus_root_path/core/common/utils:softbus_utils",
    "$hilog_lite_deps_path",
  ]
  if (ohos_kernel_type == "liteos_m") {
    static_library("softbus_trans_pending") {
      include_dirs = common_include
      public_configs = [ ":softbus_trans_pending_interface" ]
      sources = [ "src/trans_pending_pkt.c" ]
      deps = common_deps
    }
  } else {
    shared_library("softbus_trans_pending") {
      include_dirs = common_include
      cflags = [
        "-Wall",
        "-fPIC",
      ]
      public_configs = [ ":softbus_trans_pending_interface" ]
      sources = [ "src/trans_pending_pkt.c" ]
      deps = common_deps
    }
  }
} else {
  import("//build/ohos.gni")

  ohos_shared_library("softbus_trans_pending") {
    include_dirs = [
      "$dsoftbus_root_path/core/common/include",
      "$softbus_adapter_common/include",
    ]
    public_configs = [ ":softbus_trans_pending_interface" ]
    sources = [ "src/trans_pending_pkt.c" ]
    if (is_standard_system) {
      external_deps = [ "hiviewdfx_hilog_native:libhilog" ]
    }
    deps = [
      "$dsoftbus_root_path/adapter:softbus_adapter",
      "$dsoftbus_root_path/core/common/log:softbus_log",
      "$dsoftbus_root_path/core/common/utils:softbus_utils",
    ]
    part_name = "dsoftbus_standard"
    subsystem_name = "communication"
  }
}
