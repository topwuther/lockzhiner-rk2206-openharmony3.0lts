# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//foundation/communication/dsoftbus/dsoftbus.gni")

config("dsoftbus_log_interface") {
  include_dirs = [
    "$softbus_adapter_common/include",
    "$dsoftbus_root_path/core/common/include",
  ]
}

if (defined(ohos_lite)) {
  import("//build/lite/config/component/lite_component.gni")

  common_include = [
    "$dsoftbus_root_path/core/common/softbus_property/include",
    "$libsec_include_path",
    "$hilog_lite_include_path",
  ]

  if (ohos_kernel_type == "liteos_m") {
    static_library("softbus_log") {
      include_dirs = common_include
      cflags = [ "-Wall" ]
      sources = [ "softbus_log.c" ]
      deps = [
        "$dsoftbus_root_path/adapter:softbus_adapter",
        "$dsoftbus_root_path/core/common/softbus_property:softbus_property",
      ]
      public_configs = [ ":dsoftbus_log_interface" ]
    }
  } else {
    shared_library("softbus_log") {
      include_dirs = common_include
      cflags = [
        "-Wall",
        "-fPIC",
      ]
      sources = [ "softbus_log.c" ]
      public_deps = [
        "$dsoftbus_root_path/adapter:softbus_adapter",
        "$dsoftbus_root_path/core/common/softbus_property:softbus_property",
        "$hilog_lite_deps_path",
        "$libsec_deps_path",
      ]
      public_configs = [ ":dsoftbus_log_interface" ]
    }
  }
} else {
  import("//build/ohos.gni")

  ohos_shared_library("softbus_log") {
    include_dirs = [
      "$dsoftbus_root_path/core/common/softbus_property/include",
      "//utils/native/base/include",
      "include",
    ]
    deps = [
      "$dsoftbus_root_path/adapter:softbus_adapter",
      "$dsoftbus_root_path/core/common/softbus_property:softbus_property",
      "//utils/native/base:utils",
    ]
    public_configs = [ ":dsoftbus_log_interface" ]
    sources = [ "softbus_log.c" ]
    if (is_standard_system) {
      external_deps = [ "hiviewdfx_hilog_native:libhilog" ]
    }
    part_name = "dsoftbus_standard"
    subsystem_name = "communication"
  }
}
