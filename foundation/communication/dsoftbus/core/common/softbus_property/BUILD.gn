# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//foundation/communication/dsoftbus/dsoftbus.gni")

if (defined(ohos_lite)) {
  common_include = [
    "$dsoftbus_core_path/common/include",
    "include",
    "//third_party/cJSON",
    "$libsec_include_path",
    "$hilog_lite_include_path",
    "$softbus_adapter_config/spec_config",
    "//utils/native/base/include",
  ]
  if (ohos_kernel_type == "liteos_m") {
    static_library("softbus_property") {
      defines = [ "SOFTBUS_MINI_SYSTEM" ]
      include_dirs = common_include
      sources = [ "src/softbus_feature_config.c" ]
      deps = [
        "$dsoftbus_root_path/adapter:softbus_adapter",
        "//build/lite/config/component/cJSON:cjson_static",
      ]
    }
  } else {
    shared_library("softbus_property") {
      defines = [ "SOFTBUS_SMALL_SYSTEM" ]
      include_dirs = common_include
      cflags = [
        "-Wall",
        "-DDEFAULT_STORAGE_PATH=\"/usr\"",
      ]
      sources = [ "src/softbus_feature_config.c" ]
      public_deps = [
        "$dsoftbus_root_path/adapter:softbus_adapter",
        "$hilog_lite_deps_path",
        "$libsec_deps_path",
        "//build/lite/config/component/cJSON:cjson_shared",
      ]
    }
  }
} else {
  import("//build/ohos.gni")

  ohos_shared_library("softbus_property") {
    defines = [ "SOFTBUS_STANDARD_SYSTEM" ]
    sources = [ "src/softbus_feature_config.c" ]
    include_dirs = [
      "$dsoftbus_core_path/common/include",
      "include",
      "//third_party/cJSON",
      "$softbus_adapter_config/spec_config",
      "//utils/native/base/include",
    ]
    cflags = [ "-DDEFAULT_STORAGE_PATH=\"/data/data\"" ]
    if (is_standard_system) {
      external_deps = [ "hiviewdfx_hilog_native:libhilog" ]
    }
    public_deps = [
      "$dsoftbus_root_path/adapter:softbus_adapter",
      "//third_party/cJSON:cjson_static",
      "//utils/native/base:utils",
    ]
    part_name = "dsoftbus_standard"
    subsystem_name = "communication"
  }
}
