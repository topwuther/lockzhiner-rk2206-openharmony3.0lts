# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//foundation/communication/dsoftbus/dsoftbus.gni")

if (defined(ohos_lite)) {
  import("//build/lite/config/component/lite_component.gni")

  copy("permission_json") {
    sources = [ "softbus_trans_permission.json" ]
    outputs = [ "$root_out_dir/etc/softbus_trans_permission.json" ]
  }

  common_include = [
    "$dsoftbus_root_path/adapter:softbus_adapter",
    "$dsoftbus_core_path/common/include",
    "$dsoftbus_core_path/common/security/permission/include",
    "//foundation/communication/ipc_lite/interfaces/kits",
    "//base/security/permission/interfaces/kits/permission_lite",
    "//third_party/cJSON",
  ]
  if (ohos_kernel_type == "liteos_m") {
    static_library("softbus_permission") {
      include_dirs = common_include
      sources = [ "mini_system/softbus_permission.c" ]
      deps = []
    }
  } else {
    shared_library("softbus_permission") {
      include_dirs = common_include
      cflags = [
        "-Wall",
        "-fPIC",
      ]
      sources = [
        "common/permission_entry.c",
        "small_system/permission_utils.c",
        "small_system/softbus_permission.c",
      ]
      deps = [
        ":permission_json",
        "$dsoftbus_core_path/common/utils:softbus_utils",
        "$dsoftbus_root_path/adapter:softbus_adapter",
        "$dsoftbus_root_path/core/common/log:softbus_log",
        "$hilog_lite_deps_path",
        "$libsec_deps_path",
        "//base/security/permission/services/permission_lite/pms_client:pms_client",
        "//foundation/communication/ipc_lite:liteipc_adapter",
      ]
    }
  }
} else {
  import("//build/ohos.gni")
  ohos_prebuilt_etc("softbus_permission_json") {
    source = "softbus_trans_permission.json"
    install_enable = true
    relative_install_dir = "communication/softbus"
    part_name = "dsoftbus_standard"
  }

  ohos_shared_library("softbus_permission") {
    include_dirs = [
      "$dsoftbus_core_path/common/include",
      "$dsoftbus_core_path/common/security/permission/include",
      "//foundation/appexecfwk/standard/interfaces/innerkits/appexecfwk_core/include/bundlemgr",
      "//foundation/appexecfwk/standard/kits/appkit/native/app/include",
      "//foundation/appexecfwk/standard/interfaces/innerkits/appexecfwk_base/include",
      "//foundation/aafwk/standard/interfaces/innerkits/want/include",
      "//utils/system/safwk/native/include",
    ]
    sources = [
      "common/permission_entry.c",
      "standard_system/permission_utils.cpp",
      "standard_system/softbus_permission.cpp",
    ]
    deps = [
      ":softbus_permission_json",
      "$dsoftbus_core_path/common/json_utils:json_utils",
      "$dsoftbus_core_path/common/utils:softbus_utils",
      "$dsoftbus_root_path/core/common/log:softbus_log",
      "//foundation/appexecfwk/standard/kits:appkit_native",
      "//third_party/cJSON:cjson_static",
      "//utils/native/base:utils",
    ]
    external_deps = [
      "aafwk_standard:want",
      "appexecfwk_standard:appexecfwk_core",
      "hiviewdfx_hilog_native:libhilog",
      "ipc:ipc_core",
      "permission_standard:libpermissionsdk_standard",
      "samgr_L2:samgr_proxy",
    ]

    part_name = "dsoftbus_standard"
    subsystem_name = "communication"
  }
}
