# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//foundation/communication/dsoftbus/dsoftbus.gni")

if (enable_connection_ble == false) {
  sources_ble = [ "src/softbus_ble_connection_virtual.c" ]
  deps_ble = []
} else {
  sources_ble = [
    "src/softbus_ble_connection.c",
    "src/softbus_ble_trans_manager.c",
  ]
  deps_ble = [
    "$dsoftbus_root_path/adapter:softbus_adapter",
    "$dsoftbus_root_path/adapter/common/net/bluetooth:softbus_adapter_ble",
    "$dsoftbus_root_path/core/bus_center/lnn/net_ledger:dsoftbus_bus_center_ledger",
    "$dsoftbus_root_path/core/common/json_utils:json_utils",
    "$dsoftbus_root_path/core/common/log:softbus_log",
    "$dsoftbus_root_path/core/common/message_handler:message_handler",
    "$dsoftbus_root_path/core/common/queue:softbus_queue",
    "$dsoftbus_root_path/core/common/softbus_property:softbus_property",
  ]
}

if (defined(ohos_lite)) {
  import("//build/lite/config/component/lite_component.gni")

  if (ohos_kernel_type == "liteos_m") {
    static_library("ble_connection") {
      sources = sources_ble
      include_dirs = [
        "include",
        "$dsoftbus_root_path/interfaces/kits/common",
        "$dsoftbus_root_path/core/common/include",
        "$dsoftbus_root_path/core/connection/interface",
        "$dsoftbus_root_path/core/connection/manager",
      ]
      deps = deps_ble
    }
  } else {
    shared_library("ble_connection") {
      sources = sources_ble
      include_dirs = [
        "include",
        "$dsoftbus_root_path/interfaces/kits/common",
        "$dsoftbus_root_path/core/common/include",
        "$dsoftbus_root_path/core/connection/interface",
        "$dsoftbus_root_path/core/connection/manager",
      ]
      deps = deps_ble
    }
  }
} else {
  import("//build/ohos.gni")

  ohos_shared_library("ble_connection") {
    sources = sources_ble
    include_dirs = [
      "include",
      "src",
      "$dsoftbus_root_path/adapter/common/include",
      "$dsoftbus_root_path/adapter/common/net/bluetooth/include",
      "$dsoftbus_root_path/interfaces/kits/common",
      "$dsoftbus_root_path/interfaces/kits/bus_center",
      "$dsoftbus_root_path/core/common/include",
      "$dsoftbus_root_path/core/bus_center/interface",
      "$dsoftbus_root_path/core/connection/interface",
      "$dsoftbus_root_path/core/connection/manager",
      "$dsoftbus_root_path/core/connection/ble/include",
      "$dsoftbus_root_path/core/common/message_handler/include",
      "//third_party/cJSON",
    ]
    deps = deps_ble
    external_deps = [ "hiviewdfx_hilog_native:libhilog" ]
    part_name = "dsoftbus_standard"
    subsystem_name = "communication"
  }
}
